plugins {
    id 'org.springframework.boot' version '2.6.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    //npm 명령어 사용 위한 플러그인
    id "com.moowork.node" version "1.3.1"
}

group = 'me.yh'
version = '0.0.1'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //스프링부트
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    //롬복
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

//리액트 경로
def reactDir = "$projectDir" + "/frontend"

//리액트 빌드
task reactBuild(type: NpmTask) {
    workingDir = file("${reactDir}")
    args = ["run", "build"]
}

//빌드한 폴더 복사해서 붙여넣기
task copyBuild(type: Copy) {
    from 'frontend/build'
    into "build/resources/main/static"
}

copyBuild.dependsOn(reactBuild)
compileJava.dependsOn(copyBuild)